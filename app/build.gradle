plugins {
    id 'com.android.application'
}

android {
    compileSdk 34

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))
        def name = versionProps['VERSION_NAME'].toString()
        def code = Integer.valueOf(versionProps['VERSION_CODE'].toString())

        defaultConfig {
            applicationId "ch.laurinmurer.selecator"
            minSdk 30
            targetSdk 34
            versionCode code
            versionName name

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    increaseVersionCode()

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'ch.laurinmurer.selecator'
}

def increaseVersionCode() {
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(bundleRelease)) {
            /* when run release task */
            def versionPropsFile = file('version.properties')
            if (versionPropsFile.canRead()) {
                Properties versionProps = new Properties()

                versionProps.load(new FileInputStream(versionPropsFile))

                def code = Integer.valueOf(versionProps['VERSION_CODE'].toString()) + 10

                versionProps['VERSION_CODE'] = code.toString()
                versionProps.store(versionPropsFile.newWriter(), null)
            } else {
                throw new GradleException("Could not read version.properties!")
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.7.4'
    implementation 'androidx.navigation:navigation-ui:2.7.4'
    implementation 'com.github.1tchy:android-file-chooser-for-with-permission-MANAGE_EXTERNAL_STORAGE:main-SNAPSHOT'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}